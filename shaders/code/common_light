#include "code/u8x2_u16"
#include "code/u8x4"
#include "code/normal"
#include "code/sort4"


inline float3 inner_light(float2 uv, float depth, float3 normal, float4 colour_spec)
{
	float3 colour = colour_spec.rgb;
	float specular = colour_spec.a;

	float z_neg = -z_near / (1 - depth);
	float4 surface_pos = float4( uv_to_ray(uv) * z_neg, z_neg, 1.0 );
	float4 reprojected = mul(viewI_light_view_proj, surface_pos);

	float3 lightvec = light_pos - surface_pos.xyz;
	float l = length(lightvec);
	float fade = smoothstep(radius, radius * 0.9, l);
	if (fade == 0.0) return 0;

	float lighted = SHADOW_TEST;

    float radiance = lighted * fade *
		max(0.0, dot( lightvec, normal )) / (l * l * l) * light_scale;

	return radiance * colour * light_colour;
}

float4 common_light(PPosition input)
{
	float2 uv = input.svposition.xy * rcpres;

	float depth = zbuffer.Sample(sm_point, uv).x;
	float3 normal = normal_decode(gbuffer0.Sample(sm_point, uv).xy);
	float4 colour = gbuffer1.Sample(sm_point, uv);

	float3 result = inner_light(uv, depth, normal, colour);

	uint start = 4 * (input.svposition.y * res.x + input.svposition.x + 2);
	uint index = start_buffer.Load(start);
	if (index == 0) return float4(result, 1.0);

	uint N = index >> 24;
	index = index & 0x00ffffff;

	[loop] [allow_uav_condition]
	for (uint j = 0; j < N; j++)
	{
		OITFragment fragment = consolidated_buffer[index + j];
		float3 unpacked = u8x2_u16_unpack(fragment.normal16_depth16);
		normal = normal_decode(unpacked.xy);
		depth = unpacked.z;
		colour = u8x4_unpack(fragment.colour);
		result *= (1.0 - colour.a); // alpha
		colour.a = (fragment.spec8_next24 >> 24) / 255.0; //specular
		result += inner_light(uv, depth, normal, colour);
	}

	return float4(result, 1.0);
}